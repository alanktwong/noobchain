apply plugin: "groovy"

dependencies {
    // mandatory dependencies for using Spock
    compile "org.codehaus.groovy:groovy-all:2.4.15"
    testCompile "org.spockframework:spock-core:1.2-groovy-2.4"

    // optional dependencies for using Spock
    testCompile "org.hamcrest:hamcrest-core:1.3"  // only necessary if Hamcrest matchers are used
    testRuntime "net.bytebuddy:byte-buddy:1.8.21" // allows mocking of classes (in addition to interfaces)
    testRuntime "org.objenesis:objenesis:2.6"     // allows mocking of classes without default constructor (together with CGLIB)

    // dependencies used by examples in this project
    testRuntime "com.h2database:h2:1.4.197"
}

// the remaining configuration is specific to this project, and is not required for using Spock

// apply from: "gradle/ide.gradle"
apply plugin: "idea"
apply plugin: "eclipse"

def gitIgnore = file(".gitignore").readLines()
def gitIgnoreDirs = gitIgnore*.trim().findAll { !it.startsWith("#") && it.endsWith("/") }

idea {
    project {
        jdkName "1.6"
        configure(modules) {
            excludeDirs = files(gitIgnoreDirs) as Set
        }
        ipr {
            withXml { provider ->
                def node = provider.asNode()
                node.component.find { it.'@name' == 'VcsDirectoryMappings' }?.mapping[0].'@vcs' = 'Git'
            }
        }
    }
}

task collectJars(type: Copy) {
    into "$buildDir/output/lib"
    from configurations.testRuntime
}
